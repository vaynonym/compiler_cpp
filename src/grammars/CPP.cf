PDefs . Program ::= [Def];

-- Definitions
DFun   . Def ::= Fun;
DTLS   . Def ::= TLStm;
DUsing . Def ::= "using" QConstant ";";

-- Functions
FDec       . Fun       ::= Type Id "(" [Arg] ")" FRest ;
FDecInline . Fun       ::= "inline" Type Id "(" [Arg] ")" FRest ;
FRestB     . FRest     ::= "{" [Stm] "}";
FRestE     . FRest     ::= ";" ;
FArg       . Arg       ::= Type ArgRest;
FArgConst  . Arg       ::= "const" Type ArgRest;
AREmpty    . ArgRest   ::= ;
ARId       . ArgRest   ::= Id ArgDef;
FArgDef    . ArgDef    ::= "=" Exp;
FArgDefEmp . ArgDef    ::= ;


-- Qualified Constants and templates
QCDef . QConstant    ::= Id "::" QConstant;
QCId  . QConstant    ::= Id;
QCTem . QConstant    ::= Id "<" [TemplateType] ">";
TTTT  . TemplateType ::= Type; 

-- Statements
STopLevel  . Stm ::= TLStm ;
SEmpty     . Stm ::= ";" ;
SExp       . Stm ::= Exp ";" ;
SReturn    . Stm ::= "return" Exp ";" ;
SWhile     . Stm ::= "while" "(" Exp ")" Stm ;
SDoWhile   . Stm ::= "do" Stm "while" "(" Exp ")" ";" ;
SFor       . Stm ::= "for" "(" VarDecl Exp ";" Exp ")" Stm ;
SIf        . Stm ::= "if" "(" Exp ")" Stm ;
SIfElse    . Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
SBlock     . Stm ::= "{" [Stm] "}" ;

SVarDeclTL . TLStm ::= VarDecl ;
STypeDef   . TLStm ::= "typedef" Type Id ";" ;
SStruct    . TLStm ::= "struct" Id "{" [VarDecl] "}" ";" ;

SVarDecl         . VarDecl         ::= Type [VDVar] ";" ;
SVarDecl         . VarDecl         ::= "const" Type [VDVar] ";" ;
SVarDeclCInit    . VarDecl         ::= "constinit" Type [VDVar] ";" ;
SVarDeclCInit    . VarDecl         ::= "constinit" "const" Type [VDVar] ";" ;
VDVarNoInit      . VDVar           ::= Id ;
VDVarInit        . VDVar           ::= Id "=" Exp ;

-- Expressions
-- Literals
LInt      . Exp15 ::= Integer ;
LFloat    . Exp15 ::= Double ;
LChar     . Exp15 ::= Char ;
LString   . Exp15 ::= [String] ;
LTrue     . Exp15 ::= "true" ;
LFalse    . Exp15 ::= "false" ;
-- qualified constants 
EQuaConst . Exp15 ::= QConstant ;
-- indexing 
EIndex    . Exp14 ::= Exp14 "[" Exp "]" ;
-- function call
ECall     . Exp14 ::= Exp14 "(" [Exp] ")" ;
-- structure projection
EPoint    . Exp14 ::= Exp14 "." Exp15 ;
EArrow    . Exp14 ::= Exp14 "->" Exp15 ;
-- in/decrement
EPostIncr . Exp14 ::= Exp15 "++" ;
EPostDecr . Exp14 ::= Exp15 "--" ;
-- in/decrement, dereference, negation
EPreIncr  . Exp13 ::= "++" Exp14 ;
EPreDecr  . Exp13 ::= "--" Exp14 ;
EDeref    . Exp13 ::= "*"  Exp14 ;
ENeg      . Exp13 ::= "!"  Exp14 ;
-- unary plus/minus
EUnPlus   . Exp13 ::= "+"  Exp14 ;
EUnMinus  . Exp13 ::= "-"  Exp14 ;
-- Multiplication, Division,Remainder 
EMul      . Exp12 ::= Exp12 "*"  Exp13 ;
EDiv      . Exp12 ::= Exp12 "/"  Exp13 ;
EMod      . Exp12 ::= Exp12 "%"  Exp13 ; 
-- Addition, Subtraction 
EAdd      . Exp11 ::= Exp11 "+"  Exp12 ;
ESub      . Exp11 ::= Exp11 "-"  Exp12 ;
-- left and right shift
ERShift   . Exp10 ::= Exp10 ">>"  Exp11 ;
ELShift   . Exp10 ::= Exp10 "<<"  Exp11 ;
--three-way comparison
ETWComp   . Exp9  ::= Exp9 "<=>" Exp10 ;
-- Comparison
ELt       . Exp9  ::= Exp9  "<"  Exp10 ;
EGt       . Exp9  ::= Exp9  ">"  Exp10 ;
ELtEq     . Exp9  ::= Exp9  "<=" Exp10 ;
EGtEq     . Exp9  ::= Exp9  ">=" Exp10 ;
-- (in)equality 
EEq       . Exp8  ::= Exp8  "==" Exp9 ;
ENEq      . Exp8  ::= Exp8  "!=" Exp9 ;
-- Conjunction
EAnd      . Exp4  ::= Exp4  "&&" Exp5 ;
-- Disjunction 
EOr       . Exp3  ::= Exp3  "||" Exp4 ;
-- Assignment 
EAssign   . Exp2  ::= Exp3 "="   Exp2 ;
EAddAss   . Exp2  ::= Exp3 "+="  Exp2 ;
ESubAss   . Exp2  ::= Exp3 "-="  Exp2 ;
-- Conditional 
ECond     . Exp2  ::= Exp3 "?" Exp2 ":" Exp2 ;
--Exception 
EExcep    . Exp1  ::= "throw" Exp2 ;
coercions Exp 15 ;

-- Types
--TPre       . PrefixedType ::= "const" Type ;
--TNoPre     . PrefixedType ::= Type ;
TAddr      . Type         ::= Type "&" ;
TQCon      . Type         ::= QConstant ;
TInt       . Type         ::= "int" ;
TBool      . Type         ::= "bool" ;
TChar      . Type         ::= "char" ;
TDouble    . Type         ::= "double";
TVoid      . Type         ::= "void";




-- Separators and terminators here
terminator Def "";
separator Stm "";
separator Arg ",";
separator nonempty VDVar ",";
separator VarDecl "";
separator nonempty TemplateType ",";
separator Type ",";
separator Exp ",";
separator nonempty String "";

--identifiers
token Id (letter (letter|digit|'_')*) ;
-- comments
comment "//";
comment "/*" "*/";
comment "#";
